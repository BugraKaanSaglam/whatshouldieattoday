plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader -> localProperties.load(reader) }
}

def pubspecVersion() {
    def pubspecFile = rootProject.file("../pubspec.yaml")
    if (!pubspecFile.exists()) return null
    def versionLine = pubspecFile.readLines("UTF-8").find { it.trim().startsWith("version:") }
    if (versionLine == null) return null
    def cleaned = versionLine.replace("version:", "").trim()
    if (cleaned.isEmpty()) return null
    def parts = cleaned.split("\\+")
    def name = parts[0]
    def code = (parts.size() > 1 ? parts[1] : "1")
    return [name: name, code: code]
}

def pubspec = pubspecVersion()
def flutterVersionCode = (pubspec?.code ?: localProperties.getProperty("flutter.versionCode", "1")).toInteger()
def flutterVersionName = pubspec?.name ?: localProperties.getProperty("flutter.versionName", "1.0")

android {
    namespace = "com.whatshouldieattoday.mobile"
    compileSdk = 36
    ndkVersion = "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId = "com.whatshouldieattoday.mobile"
        minSdkVersion = flutter.minSdkVersion
        targetSdk = 36
        versionCode = flutterVersionCode
        versionName = flutterVersionName
    }

    def keystoreProperties = new Properties()
    def keystorePropertiesFile = rootProject.file("key.properties")
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.release
            minifyEnabled = false
            shrinkResources = false
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}
